plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.gradleup.shadow" version "8.3.5"
}

group = 'dev.aevorinstudios'
version = '1.0.0-beta-1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    implementation "org.mindrot:jbcrypt:0.4"
    implementation "com.zaxxer:HikariCP:5.1.0"
    implementation "org.json:json:20240303"
    compileOnly "com.comphenix.protocol:ProtocolLib:5.3.0"
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.1")
    }
    
    shadowJar {
        relocate 'org.mindrot.jbcrypt', 'dev.aevorinstudios.aevorinAuth.libs.jbcrypt'
        relocate 'com.zaxxer.hikari', 'dev.aevorinstudios.aevorinAuth.libs.hikari'
        relocate 'org.json', 'dev.aevorinstudios.aevorinAuth.libs.json'
    }
}

jar {
    enabled = false
}

// Make sure build uses shadowJar
build.dependsOn shadowJar

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
